# EBNF-like grammar of aerospace CLI args to generate shell completion. Managed by https://github.com/adaszko/complgen
aerospace <subcommand>;
aerospace -v;
aerospace --version;
aerospace --help;
aerospace -h;

<subcommand> ::= balance-sizes

    | close [--quit-if-last-window]

    | close-all-windows-but-current [--quit-if-last-window]

    | enable (on|off|toggle)

    | flatten-workspace-tree

    | focus [<focus_flag>]... (left|down|up|right) [<focus_flag>]...
    | focus --window-id <window_id>

    | focus-back-and-forth

    | focus-monitor [--wrap-around] (left|down|up|right) [--wrap-around]
    | focus-monitor [--wrap-around] (next|prev) [--wrap-around]
    | focus-monitor <monitor_pattern>...

    | fullscreen [on|off]

    | join-with (left|down|up|right)

    | layout (h_tiles|v_tiles|h_accordion|v_accordion|tiles|accordion|horizontal|vertical|tiling|floating)...

    | macos-native-fullscreen [on|off]

    | macos-native-minimize

    | mode <mode_id>

    | move (left|down|up|right)

    | move-mouse (monitor-lazy-center|monitor-force-center|window-lazy-center|window-force-center)

    | move-node-to-monitor [--wrap-around] (left|down|up|right) [--wrap-around]
    | move-node-to-monitor [--wrap-around] (next|prev) [--wrap-around]
    | move-node-to-monitor <monitor_pattern>...

    | move-node-to-workspace [--wrap-around] (next|prev) [--wrap-around]
    | move-node-to-workspace <workspace>

    | move-workspace-to-monitor [--wrap-around] (next|prev) [--wrap-around]

    | reload-config [--no-gui | --dry-run]...

    | resize (smart|width|height) [+|-]<number>

    | split (horizontal|vertical|opposite)

    | trigger-binding <binding> --mode <mode_id>
    | trigger-binding --mode <mode_id> <binding>

    | workspace [--auto-back-and-forth] <workspace> [--auto-back-and-forth]
    | workspace [--wrap-around] (next|prev) [--wrap-around]

    | workspace-back-and-forth

    | config [--json | --keys]... --get {{{ aerospace config --major-keys }}} [--json | --keys]...
    | config --major-keys
    | config --all-keys
    | config --config-path

    | debug-windows

    | list-apps [--macos-native-hidden [no] | --format <output_format>]...

    | list-exec-env-vars

    | list-monitors [--focused [no]] [--mouse [no]] | list-monitors [--mouse [no]] [--focused [no]]

    | list-windows [<list_windows_filter_flag> | --format <output_format>]...
    | list-windows [--format <output_format>] --all [--format <output_format>]
    | list-windows [--format <output_format>] --focused [--format <output_format>]

    | list-workspaces [<list_workspaces1_flag>]... --monitor <monitor_id>... [<list_workspaces1_flag>]...
    | list-workspaces [--format <output_format>] --all [--format <output_format>]
    | list-workspaces [--format <output_format>] --focused [--format <output_format>]
    ;

<window_id> ::= {{{ aerospace list-windows --all --format '%{window-id}%{right-padding}%{tab}%{app-name}%{right-padding} | %{window-title}' }}};
<binding> ::= {{{ aerospace config --get mode --keys | xargs -I{} aerospace config --get mode.{}.binding --keys }}};
<mode_id> ::= {{{ aerospace config --get mode --keys }}};
<workspace> ::= {{{ aerospace list-workspaces --monitor all --empty no }}};
<number> ::= {{{ true }}};
<monitor_pattern> ::= {{{ true }}};

<list_workspaces1_flag> ::= --visible [no] | --empty [no] | --format <output_format>;

<focus_flag> ::= --boundaries <boundary>|--boundaries-actions <boundaries_action>|--ignore-floating;
<boundaries_action> ::= stop|wrap-around-the-workspace|wrap-around-all-monitors;
<boundary> ::= workspace|all-monitors-outer-frame;

<list_windows_filter_flag> ::= --workspace (visible | focused | <workspace>)...
    | --monitor <monitor_id>...
    | --pid <pid>
    | --app-bundle-id <app_bundle_id>
    ;
<output_format> ::= {{{ true }}};

<app_bundle_id> ::= {{{ aerospace list-apps --format '%{app-bundle-id}%{right-padding}%{tab}%{app-name}' }}};
<pid> ::= {{{ aerospace list-apps --format '%{app-pid}%{right-padding}%{tab}%{app-name}' }}};
<monitor_id> ::= all | mouse | focused | {{{ aerospace list-monitors --format '%{monitor-id}%{right-padding}%{tab}%{monitor-name}' }}};
